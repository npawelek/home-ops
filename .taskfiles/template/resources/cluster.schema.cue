package config

import (
	"net"
)

#Config: {
	node_cidr: net.IPCIDR & !=cluster_pod_cidr & !=cluster_svc_cidr
	node_dns_servers?: [...net.IPv4]
	node_ntp_servers?: [...net.IPv4]
	node_default_gateway?: net.IPv4 & !=""
	node_vlan_tag?: string & !=""
	cluster_pod_cidr: *"10.42.0.0/16" | net.IPCIDR & !=node_cidr & !=cluster_svc_cidr
	cluster_svc_cidr: *"10.43.0.0/16" | net.IPCIDR & !=node_cidr & !=cluster_pod_cidr
	cluster_api_addr: net.IPv4
	cluster_api_tls_sans?: [...net.FQDN]
	cluster_gateway_internal_addr: net.IPv4 & !=cluster_api_addr
	cluster_domain: string & !=""
	repository_name: string
	repository_branch?: string & !=""
	repository_visibility?: *"public" | "private"
	certmanager_aws_access_key_id: string & !=""
	certmanager_aws_secret_key: string & !=""
	externaldns_aws_access_key_id: string & !=""
	externaldns_aws_secret_key: string & !=""
	aws_region: string & !=""
	cilium_bgp_router_addr?: net.IPv4 & !=""
	cilium_bgp_router_asn?: string & !=""
	cilium_bgp_node_asn?: string & !=""
	cilium_loadbalancer_mode?: *"dsr" | "snat"
}

#Config
